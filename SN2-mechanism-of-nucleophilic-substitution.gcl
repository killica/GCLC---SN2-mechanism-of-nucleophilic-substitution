number pi 3.14159
dim 300 300
ang3d_picture -500 -500 500 500
ang3d_origin 100 50 pi 0.7
ang3d_unit 2

ang3d_axes_drawing_range -50 50 -50 50 -50 50

% ----------------
% Central carbon
% ----------------

procedure draw_central_carbon {x} 
{
color 100 100 100
ang3d_draw_parametric_surface u v 
{0; u <= 6.28; u + 0.1} 
{-1.57; v <= 1.57; v + 0.06} 
{ 20+10*cos(v)*cos(u);  20+10*cos(v)*sin(u); 20 +10*sin(v) }

color 0 0 0
fontsize 40
ang3d_point C 20 20 20
printat C {C}
fontsize 20
}


% --------------
% Leaving group animation
% --------------

procedure draw_leaving_group {x} {
linethickness 0.16
color 137 207 240
ang3d_draw_parametric_surface u v
{0; u <= 6.28; u + 0.1}
{-1.57; v <= 1.57; v + 0.06}
{x + 5*cos(v)*cos(u); 20 + 5*cos(v)*sin(u); 20 + 5*sin(v)}

color 0 0 0
ang3d_point LG x 20 20
if_then_else {x == 45} 
{
printat LG {LG}
}
{
printat LG {LG:}
}

}

% --------------
% Nucleophile animation
% --------------

procedure draw_nucleophile {x} {
linethickness 0.16
color 228 208 10
ang3d_draw_parametric_surface u v
{0; u <= 6.28; u + 0.1}
{-1.57; v <= 1.57; v + 0.06}
{ x+ 5*cos(v)*cos(u); 20 + 5*cos(v)*sin(u); 20 + 5*sin(v) }
color 0 0 0
ang3d_point Nu x 20 20
if_then_else {x < -5} 
{
printat Nu {:Nu}
}
{
printat Nu {Nu}
}

}

procedure draw_nucleophile_bond {}
{
color 218 165 32
ang3d_draw_parametric_surface u v
{0; u <= 6.28; u + 0.3} 
{0; v <= 10; v + 0.3} 
{ v; 20 + 1*cos(u); 20 + 1*sin(u) }
}

procedure draw_leaving_group_bond {} 
{
color 0 0 100
ang3d_draw_parametric_surface u v
{0; u <= 6.28; u + 0.3} 
{30; v <= 40; v + 0.3} 
{ v; 20 + 1*cos(u); 20 + 1*sin(u) }
}

procedure draw_ligand_bond {x y z} 
{
number R_C 10 
number R_L 5

expression dx {20 - x}
expression dy {20 - y}
expression dz {20 - z}

expression D {sqrt(dx*dx + dy*dy + dz*dz)}

expression PLx {x + R_L * dx / D}
expression PLy {y + R_L * dy / D}
expression PLz {z + R_L * dz / D}

expression PCx {20 - R_C * dx / D}
expression PCy {20 - R_C * dy / D}
expression PCz {20 - R_C * dz / D}

linethickness 0.5
color 0 0 100
ang3d_draw_parametric_curve t
{0; t <= 1; t + 0.01}
{
  PLx + t * (PCx - PLx);
  PLy + t * (PCy - PLy);
  PLz + t * (PCz - PLz)
}

}

procedure draw_R {x y z}
{
color 200 200 200
linethickness 0.16
ang3d_draw_parametric_surface u v
{0; u <= 6.28; u + 0.1}
{-1.57; v <= 1.57; v + 0.06}
{ x + 5*cos(v)*cos(u); y + 5*cos(v)*sin(u); z + 5*sin(v) }

color 0 0 0
ang3d_point R x y z
if_then_else {z == 20}
{
printat R {R1}
}
{
if_then_else {z == 40.41}
{
printat R {R2}
}
{
printat R {R3}
}
}

call draw_ligand_bond {x y z}
}

procedure animation_at_t {t} {
expression pi {3.1416}
expression alfa {pi/3 - t/200 * pi}  
expression beta {-0.4 + t/100 * 0.5}   
ang3d_origin 150 50 alfa beta
call draw_central_carbon {0}

if_then_else {t <= 30} 
{
call draw_leaving_group_bond {}
call draw_leaving_group {45}
expression x {t/2 - 20}
call draw_nucleophile {x}
call draw_R {11.67 43.57 20}
call draw_R {11.67 8.215 40.41}
call draw_R {11.67 8.215 -0.41}
}
{
call draw_nucleophile {-5}
call draw_nucleophile_bond {}
if_then_else {t <= 65}
{
call draw_leaving_group_bond {}
call draw_leaving_group {45}
expression x {0.555333 * t - 7.7666}
call  draw_R {x 43.57 20}
call  draw_R {x 8.215 40.41}
call  draw_R {x  8.215 -0.41}
}

{
expression x {t/2 + 10}
call draw_leaving_group {x}
call  draw_R {28.33 43.57 20}
call  draw_R {28.33 8.215 40.41}
call  draw_R {28.33  8.215 -0.41}
}
 
}
}

point TIME 0 0 100 100
getx x TIME
call animation_at_t {x}
animation_frames 50 15

